import org.gradle.internal.jvm.Jvm

buildscript {
  dependencies {
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.0'
    classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.5.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.18.0'
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'com.vanniktech.maven.publish'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'junit:junit:4.13.1'
}

// Animal Sniffer only works on JDK 11 or older currently.
if (!Jvm.current().javaVersion.isJava12Compatible()) {
  apply plugin: 'ru.vyarus.animalsniffer'

  animalsniffer {
    sourceSets = [sourceSets.main] // Only check main sources, ignore test code.
  }
  dependencies {
    signature 'org.codehaus.mojo.signature:java18:1.0@signature'
    signature 'net.sf.androidscents.signature:android-api-level-21:5.0.1_r2@signature'
  }
}
